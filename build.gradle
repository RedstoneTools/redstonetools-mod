plugins {
	id 'fabric-loom' version '1.11-SNAPSHOT'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '8.+'
	id "me.modmuss50.mod-publish-plugin" version "1.1.0"
}

version = project.mod_version + "+" + stonecutter.current.version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

loom {
	splitEnvironmentSourceSets()

	mods {
		redstonetools {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

stonecutter {
	swaps["mouse_clicked_params"] = eval(current.version, "<1.21.10") ? "double mouseX, double mouseY, int button" : "Click click, boolean doubleClick" // mouseClicked
	swaps["mouse_clicked_args"] = eval(current.version, "<1.21.10") ? "mouseX, mouseY, button" : "click, doubleClick"
	swaps["on_mouse_clicked_args"] = eval(current.version, "<1.21.10") ? "(int) mouseX, (int) mouseY, button" : "click, doubleClick" // onMouseClicked
	swaps["dragged_released_params"] = eval(current.version, "<1.21.10") ? "double mouseX, double mouseY, int button" : "Click click" // mouseDragged, mouseReleased
	swaps["dragged_released_args"] = eval(current.version, "<1.21.10") ? "mouseX, mouseY, button" : "click"
	swaps["on_released_args"] = eval(current.version, "<1.21.10") ? "(int) mouseX, (int) mouseY, button" : "click" // onMouseReleased
	swaps["keyinput_params"] = eval(current.version, "<1.21.10") ? "int keyCode, int scanCode, int modifiers" : "KeyInput input" // keyPressed, keyReleased
	swaps["keyinput_args"] = eval(current.version, "<1.21.10") ? "keyCode, scanCode, modifiers" : "input"
	swaps["charinput_params"] = eval(current.version, "<1.21.10") ? "char chr, int modifiers" : "CharInput input" // charTyped
	swaps["charinput_args"] = eval(current.version, "<1.21.10") ? "chr, modifiers" : "input"
	swaps["click_and_inputs_imports"] = eval(current.version, "<1.21.10") ? "" :
		"//\nimport net.minecraft.client.gui.Click;\nimport net.minecraft.client.input.KeyInput;\nimport net.minecraft.client.input.CharInput;"

	swaps["get_x"] = eval(current.version, "<1.21.10") ? "x" : "getX()"
	swaps["get_y"] = eval(current.version, "<1.21.10") ? "y" : "getY()"
	swaps["get_width"] = eval(current.version, "<1.21.10") ? "entryWidth" : "getWidth()"
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
	maven { url 'https://masa.dy.fi/maven' }
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		name = "WorldEdit Maven"
		url = "https://maven.enginehub.org/repo/"
	}
	maven {
		name = "JitPack"
		url = "https://jitpack.io"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft("com.mojang:minecraft:${project.minecraft_version}")
	mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
	modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

	// Worldedit API
	modImplementation("com.sk89q.worldedit:worldedit-fabric-mc${project.worldedit_version}")
	// MaLiLib
	modApi "maven.modrinth:malilib:${project.malilib_version}"
}

loom {
	decompilerOptions.named("vineflower") {
		options.put("mark-corresponding-synthetics", "1") // Adds names to lambdas - useful for mixins
	}

	runConfigs.configureEach {
		ideConfigGenerated(true)
//		vmArgs("-Dmixin.debug.export=true") // Exports transformed classes for debugging
		runDir = "../../run" // Shares the run directory between versions
	}
}

processResources {
	inputs.property "version", project.version
	inputs.property "minecraft_version", project.minecraft_version
	inputs.property "minecraft_version_out", project.minecraft_version_out
	inputs.property "malilib_version", project.minecraft_version_out
	inputs.property "loader_version", project.loader_version
	filteringCharset "UTF-8"

	filesMatching("fabric.mod.json") {
		expand(
				"version": project.version,
				"minecraft_version": project.minecraft_version,
				"minecraft_version_out": project.minecraft_version_out,
				"malilib_version": project.minecraft_version_out,
				"loader_version": project.loader_version
		)
	}
}

tasks.register("collectFile") {
	group = "build"
	mustRunAfter("build")

	doLast {
//		println "build/libs/${project.archives_base_name}-${project.mod_version}+${project.minecraft_version}.jar"
		copy {
			from file("build/libs/${project.archives_base_name}-${project.mod_version}+${project.minecraft_version}.jar")
			into rootProject.file("build/libs")
		}
	}
}

tasks.register("buildAndCollect") {
	group = "build"
	dependsOn(tasks.named("build"), tasks.named("collectFile"))
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"
	it.options.release.set(21)
}

java {
	toolchain.languageVersion = JavaLanguageVersion.of(21)
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

publishMods {
	github {
		accessToken = providers.environmentVariable("GITHUB_TOKEN")
		repository = "RedstoneTools/redstonetools-mod"
		commitish = "main"
		tagName = "v${project.mod_version}"
		allowEmptyFiles = true
	}
}